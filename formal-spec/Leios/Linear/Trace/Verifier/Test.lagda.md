## Trace verification with sample trace
<!--
```agda
open import Prelude.Errors
open import Prelude.Result
open import Leios.Prelude hiding (id)
open import Leios.Config
open import Leios.SpecStructure using (SpecStructure)
```
-->
```agda
module Leios.Linear.Trace.Verifier.Test where
private
```
#### Params
A test setup requires parameters, which are bundled in the `Params` record. There are the network parameters, the identifier for the system under test (SUT)
and there are the schedules for block production and voting in the field `winning-slots`. Note, in Linear Leios EB are produced optionaly together with an RB.
```agda
  params : Params
  params =
    record
      { networkParams =
          record
            { numberOfParties = 2
            ; stakeDistribution =
                let open FunTot (completeFin 2) (maximalFin 2)
                in Fun⇒TotalMap (const 100000000)
            ; stageLength = 2
            ; ledgerQuality = 0
            ; lateIBInclusion = false
            }
      ; sutId = fzero
      ; winning-slots = fromList $
                     (VT , 100) ∷
                     (VT , 104) ∷
                     (VT , 105) ∷
                     (VT , 106) ∷
        (EB , 107) ∷ (VT , 107) ∷
        (EB , 108) ∷ (VT , 108) ∷
        []
    }
```
#### Additional Linear Leios parameters
Linear Leios has the following three protocol parameters
```agda
  Lhdr  = 1
  Lvote = 2
  Ldiff = 1
```
#### SpecStructure
In order to build a test trace, an implementation for the `SpecStructure` needs to be specified.
For the test trace, we rely on the implementation provided in `Leios.Defaults`.
```agda
  -- TODO: why does Hashable-EndorserBlock not work instead of hpe...?
  open import Leios.Defaults params using (d-SpecStructure; hpe)
  open SpecStructure d-SpecStructure hiding (Hashable-EndorserBlock)
```
#### TraceVerifier
```agda
  splitTxs : List Tx → List Tx × List Tx
  splitTxs l = [] , l

  validityCheckTime : EndorserBlock → ℕ
  validityCheckTime eb = 4

  open import Leios.Linear.Trace.Verifier d-SpecStructure params Lhdr Lvote Ldiff splitTxs validityCheckTime
```
<!--
```agda
  open Params params
  open Types params
  open GenFFD

  opaque
    unfolding List-Model
    unfolding isValid?
```
-->
EndorserBlocks that will be used in the test trace.
```agda
    EB₁ : EndorserBlock
    EB₁ = mkEB 100 (fsuc fzero) tt (EB , tt) (3 ∷ 4 ∷ 5 ∷ []) [] []
```
Checking `hash` of EndorserBlocks
```agda
    verify-EB₁-hash : hash EB₁ ≡ 3 ∷ 4 ∷ 5 ∷ []
    verify-EB₁-hash = refl
```
RankingBlocks that will be used in the test trace
```agda
    RB₀ RB₁ RB₂ : RankingBlock
    RB₀ = record { txs = 3 ∷ 4 ∷ 5 ∷ [] ; announcedEB = nothing ; ebCert = nothing }
    RB₁ = record { txs = [] ; announcedEB = just (hash EB₁) ; ebCert = nothing }
    RB₂ = record { txs = [] ; announcedEB = nothing ; ebCert = just (hash EB₁) }
```
Starting at slot 100
```agda
    s₁₀₀ : LeiosState
    s₁₀₀ = record s₀
             { slot = 100
             ; ToPropose = 0 ∷ 1 ∷ 2 ∷ []
             ; PubKeys = (fzero , tt) ∷ (fsuc fzero , tt) ∷ []
             }
      where
        s₀ : LeiosState
        s₀ = initLeiosState tt stakeDistribution ((fzero , tt) ∷ (fsuc fzero , tt) ∷ [])
```
### Build a test trace
```agda
    test-trace : TestTrace
    test-trace =
```
<pre>
slot: 100 101 102 103 104 105 106 107

            3* Lhdr    Lvote
          <---------> <-----> <->
  ___|___|___|___|___|___|___|___|___|___

                     +---+
                     |VT₁|
                     +---+
     +---+             |         +---+
     |RB₁|             |         |RB₂|
     |EB₁| <-----------+---------|CRT|
     +---+                       +---+
</pre>
#### Slot 100
Submitting transactions, Receiving RB₀
```agda
                   (No-EB-Role-Action 100 , inj₁ SLOT)
                 ∷ (Base₁-Action      100 , inj₂ (inj₂ (SubmitTxs (3 ∷ 4 ∷ 5 ∷ []))))
                 ∷ (Base₂-Action      100 , inj₁ SLOT)
                 ∷ (No-VT-Role-Action 100 , inj₁ SLOT)
                 ∷ (Slot₂-Action      100 , inj₂ (inj₁ (BASE-LDG [ RB₀ ])))
                 ∷ (Slot₁-Action      100 , inj₁ (FFD-OUT []))
```
#### Slot 101
Receiving RB₁/EB₁ generated by party 1 in slot 100
```agda
                 ∷ (Base₂-Action      101 , inj₁ SLOT)
                 ∷ (No-EB-Role-Action 101 , inj₁ SLOT)
                 ∷ (No-VT-Role-Action 101 , inj₁ SLOT)
                 ∷ (Slot₂-Action      101 , inj₂ (inj₁ (BASE-LDG [ RB₁ ])))
                 ∷ (Slot₁-Action      101 , inj₁ (FFD-OUT (inj₁ (ebHeader EB₁) ∷ [])))
```
#### Slot 102
```agda
                 ∷ (Base₂-Action      102 , inj₁ SLOT)
                 ∷ (No-EB-Role-Action 102 , inj₁ SLOT)
                 ∷ (No-VT-Role-Action 102 , inj₁ SLOT)
                 ∷ (Slot₁-Action      102 , inj₁ (FFD-OUT []))
```
#### Slot 103
```agda
                 ∷ (Base₂-Action      103 , inj₁ SLOT)
                 ∷ (No-EB-Role-Action 103 , inj₁ SLOT)
                 ∷ (No-VT-Role-Action 103 , inj₁ SLOT)
                 ∷ (Slot₁-Action      103 , inj₁ (FFD-OUT []))
```
#### Slot 104
The SUT is voting for EB₁ in this slot
```agda
                 ∷ (Base₂-Action      104 , inj₁ SLOT)
                 ∷ (No-EB-Role-Action 104 , inj₁ SLOT)
                 ∷ (VT-Role-Action 104 EB₁ 101 , inj₁ SLOT)
                 ∷ (Slot₁-Action      104 , inj₁ (FFD-OUT []))
```
#### Slot 105
```agda
                 ∷ (Base₂-Action      105 , inj₁ SLOT)
                 ∷ (No-EB-Role-Action 105 , inj₁ SLOT)
                 ∷ (No-VT-Role-Action 105 , inj₁ SLOT)
                 ∷ (Slot₁-Action      105 , inj₁ (FFD-OUT []))
```
#### Slot 106
```agda
                 ∷ (Base₂-Action      106 , inj₁ SLOT)
                 ∷ (No-EB-Role-Action 106 , inj₁ SLOT)
                 ∷ (No-VT-Role-Action 106 , inj₁ SLOT)
                 ∷ (Slot₁-Action      106 , inj₁ (FFD-OUT []))
```
#### Slot 107
```agda
                 ∷ (Base₂-Action      107 , inj₁ SLOT)
                 ∷ (Slot₂-Action      107 , inj₂ (inj₁ (BASE-LDG [ RB₂ ])))
                 ∷ []
```
#### Verify the test-trace
```agda
    test₁ : IsOk (verifyTrace (L.reverse test-trace) s₁₀₀)
    test₁ = _
```
### Test error handling
```agda
    retValue : String
    retValue =
      case verifyTrace (L.reverse test-trace) s₁₀₀ of λ where
        (Ok _) → "ok"
        (Err e) → errorDoc e

    test₂ : retValue ≡ "ok"
    test₂ = refl
```
